#!/usr/bin/env -S just --justfile

import? '~/hjdocs/public-docs/recurse-brazil/justfile'

set allow-duplicate-recipes

[private]
default:
    @just --list --unsorted

# Run `eval $(just setup_shell)` for aliases and command completion
setup_shell:
    #!/usr/bin/env zsh
    for recipe in `just --summary`
    do
        echo "alias j$recipe=\"just $recipe\""
    done

    # this has completions at the end and we want them after aliases. It doesn't work otherwise for some reason.
    [ -f $HOME/hjdocs/public-docs/recurse-brazil/justfile ] &&
        just --justfile=$HOME/hjdocs/public-docs/recurse-brazil/justfile setup_shell

    just --completions $(basename $SHELL)

setup_git:
    #!/usr/bin/env zsh
    set -xeuo pipefail

    git remote | grep '^o$' ||
        git remote add o $(git remote get-url origin)

    for r in origin o
    do
        git config unset --all remote.$r.fetch
        for rt in origin o
        do
            git config --add remote.$r.fetch +HEAD:refs/remotes/$rt/main
            git config --add remote.$r.fetch +refs/heads/$(whoami)/\*:refs/remotes/$rt/\*
        done

        # push with $(whoami)/ prefix
        git config --add remote.$r.fetch ^refs/heads/$(whoami)/dev
        git config --replace-all remote.$r.push refs/heads/\*:refs/heads/$(whoami)/\*
    done

brightness val="":
    val={{ val }}
    val=${val:-$(cat /sys/class/backlight/intel_backlight/max_brightness )}
    sudo sh -c "echo ${val} > /sys/class/backlight/intel_backlight/brightness"

# Run the given $command and run them again whenever a file changes
[no-cd]
[linux]
watch *$command:
    #!/usr/bin/env zsh
    set -euo pipefail
    set -x
    FILES_TO_WATCH=${FILES_TO_WATCH:-.}
    D=1 
    while [ -n "$(fd --changed-after @$D | head -1)" ] || (
        inotifywait -e modify -r ${FILES_TO_WATCH}
        while inotifywait -e modify -r ${FILES_TO_WATCH} -t 5
        do
            :
        done
    )
    do
        D=$(date +%s) 
        (
            set +eo pipefail
            set -x
            time zsh -ic "{{ command }}"
            echo "Exit code: $?"
        )
        date
    done
