# shortcut to this dotfiles path is $DOTFILES_ROOT
export DOTFILES_ROOT=`(cd $(dirname $(readlink $HOME/.zshrc)); cd ..; pwd)`

# your project folder that we can `c [tab]` to
export PROJECTS=~/dev

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES_ROOT/**/*.zsh)

# load the path files
for file in ${${(M)config_files:#*/path.zsh}:#*/zprezto.symlink/modules/*}
do
  echo "sourcing path from: $file"
  source $file
done

# TODO this one stops me from logging in:
# .dotfiles/vim.sync/bundle/vimshell/autoload/unite/sources/vimshell_zsh_complete/complete.zsh
#
# load everything but the path and completion files
for file in ${${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/vimshell/*}:#*/zprezto.symlink/modules/*}
do
  echo "sourcing: $file"
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  echo "sourcing completion from: $file"
  source $file
done

unset config_files

export EDITOR=vim
export VISUAL=vim
export SUDO_EDITOR=vim

# http://www.tldp.org/HOWTO/BackspaceDelete/system.html#AEN270
stty erase '^?'

export NVM_DIR="/home/hojin/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"  # This loads nvm
