# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  #source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# shortcut to this dotfiles path is $DOTFILES_ROOT
export DOTFILES_ROOT=`(cd $(dirname $(readlink $HOME/.zshrc)); cd ..; pwd)`

# your project folder that we can `c [tab]` to
export PROJECTS=~/dev

# should be initialised otherwise default values are not used
#MANPATH=${MANPATH:-$(manpath)}

# all of our zsh files
typeset -U config_files
config_files=(${DOTFILES_ROOT}/**/*.zsh)
zprezto_config_file=zprezto.symlink/init.zsh
config_files=(${config_files:#*${zprezto_config_file}})

# load the path files
for file in ${${(M)config_files:#*/path.zsh}:#*/zprezto.symlink/modules/*}
do
  #echo "sourcing path from: $file"
  source $file
done

# https://youtrack.jetbrains.com/articles/IDEA-A-19/Shell-Environment-Loading
if [ -z "$INTELLIJ_ENVIRONMENT_READER" ]; then
  #echo "sourcing: ${DOTFILES_ROOT}/$zprezto_config_file"
  source ${DOTFILES_ROOT}/$zprezto_config_file
fi

# TODO this one stops me from logging in:
# .dotfiles/vim.sync/bundle/vimshell/autoload/unite/sources/vimshell_zsh_complete/complete.zsh
#
# load everything but the path and completion files
for file in ${${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/vimshell/*}:#*/zprezto.symlink/modules/*}
do
  #echo "sourcing: $file"
  source $file
done

#source ${DOTFILES_ROOT}/zprezto.symlink/contrib/fzf-tab/fzf-tab.plugin.zsh

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  #echo "sourcing completion from: $file"
  source $file
done

unset config_files

export EDITOR=vim
export VISUAL=vim
which mvim >/dev/null 2>&1 && export VISUAL=mvim
export SUDO_EDITOR=vim

export LESS="${LESS} --quit-if-one-screen --jump-target=.4"

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

export JAVA_TOOLS_OPTIONS="-Dlog4j2.formatMsgNoLookups=true"

export AWS_EC2_METADATA_DISABLED=true
